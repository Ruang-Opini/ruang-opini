plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

ext.majorVersion = 0
ext.minorVersion = 3
ext.patchVersion = 4
ext.preRelease = "beta"
ext.minSdkVersion = 21

android {
    signingConfigs {
        release {
            storeFile file('keystore.jks')
            storePassword 'android'
            keyAlias 'ruangopini'
            keyPassword 'android'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "id.ruangopini"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion 30
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

private Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.majorVersion * 10000 + ext.minorVersion * 100 + ext.patchVersion
}

private String generateVersionName() {
    String versionName = "${ext.majorVersion}.${ext.minorVersion}.${ext.patchVersion}"
    if (ext.preRelease != null && !ext.preRelease.isEmpty()) versionName = versionName + "-" + ext.preRelease
    return versionName
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // firebase
    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    // auth gso
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    // coil
    implementation 'io.coil-kt:coil:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // ktx artifact
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.fragment:fragment-ktx:1.3.4"

    // android lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"

    // coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.0-native-mt'

    // image picker
    implementation 'com.github.dhaval2404:imagepicker:2.0'

    // sweet-alert-dialog
    implementation 'com.github.f0ris.sweetalert:library:1.6.2'

    // bottom nav
    implementation 'com.gauravk.bubblenavigation:bubblenavigation:1.0.7'

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"

    // Koin main features for Android (Scope,ViewModel ...)
    implementation 'io.insert-koin:koin-android:3.0.2'
    // Koin Android - experimental builder extensions
    implementation 'io.insert-koin:koin-android-ext:3.0.2'

    // skeleton layout
    implementation 'com.faltenreich:skeletonlayout:2.1.0'

    // data store
    implementation "androidx.datastore:datastore-preferences:1.0.0-beta01"

    // photo view
    implementation 'com.github.stfalcon:stfalcon-imageviewer:1.0.1'

    //time ago library
    implementation 'com.github.marlonlom:timeago:4.0.3'
}